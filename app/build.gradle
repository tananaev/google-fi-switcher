plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdk 34
    defaultConfig {
        applicationId 'com.tananaev.google.fi.switcher'
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 6
        versionName '2.2'
    }
    namespace 'com.tananaev.switcher'

    buildFeatures {
        flavorDimensions = ['default']
    }
    productFlavors {
        regular {
            isDefault = true
            ext.enableCrashlytics = false
        }
        google
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    buildFeatures {
        compose true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.25.1"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation "androidx.activity:activity-compose:1.8.0"
    implementation platform('androidx.compose:compose-bom:2023.10.01')
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.ui:ui-tooling-preview"
    debugImplementation "androidx.compose.ui:ui-tooling"
    googleImplementation platform('com.google.firebase:firebase-bom:32.5.0')
    googleImplementation 'com.google.firebase:firebase-analytics-ktx'
    googleImplementation 'com.google.firebase:firebase-crashlytics'
    googleImplementation 'com.google.android.gms:play-services-ads:22.5.0'
    googleImplementation 'com.google.android.play:review-ktx:2.0.1'
}

tasks.register('copyFirebaseConfig', Copy) {
    from '../../environment/firebase'
    into '.'
    include 'google-fi-switcher.json'
    rename('google-fi-switcher.json', 'google-services.json')
}
afterEvaluate {
    preGoogleDebugBuild.mustRunAfter copyFirebaseConfig
    preGoogleReleaseBuild.mustRunAfter copyFirebaseConfig
}
android.applicationVariants.configureEach { variant ->
    tasks.named("process${variant.name.capitalize()}GoogleServices")
            .get().enabled(variant.flavorName == 'google')
}
