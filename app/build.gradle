plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId 'com.tananaev.google.fi.switcher'
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 6
        versionName '2.2'
    }
    namespace 'com.tananaev.switcher'

    flavorDimensions 'default'
    productFlavors {
        regular {
            isDefault = true
        }
        google
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.25.1"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.0-alpha02'
    implementation "androidx.activity:activity-compose:1.5.0"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    googleImplementation platform('com.google.firebase:firebase-bom:31.0.0')
    googleImplementation 'com.google.firebase:firebase-analytics-ktx'
    googleImplementation 'com.google.firebase:firebase-crashlytics'
    googleImplementation 'com.google.android.gms:play-services-ads:21.3.0'
    googleImplementation 'com.google.android.play:review-ktx:2.0.0'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains('Google')) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'

    task copyJson(type: Copy) {
        from '../../environment/firebase'
        into '.'
        include 'google-fi-switcher.json'
        rename('google-fi-switcher.json', 'google-services.json')
    }

    afterEvaluate {
        preBuild.dependsOn copyJson
    }
}
